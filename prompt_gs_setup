# prompt style and colors based on Steve Losh's Prose theme:
# http://github.com/sjl/oh-my-zsh/blob/master/themes/prose.zsh-theme
#
# Requires the `git-info` zmodule to be included in the .zimrc file.

prompt_gs_git() {
  [[ -n ${git_info} ]] && print -n " ${(e)git_info[prompt]}"
}

prompt_gs_virtualenv() {
  [[ -n ${VIRTUAL_ENV} ]] && print -n " (%F{blue}${VIRTUAL_ENV:t}%f)"
}

prompt_gs_precmd() {
  (( ${+functions[git-info]} )) && git-info
}

prompt_gs_setup() {
  [[ -n ${VIRTUAL_ENV} ]] && export VIRTUAL_ENV_DISABLE_PROMPT=1

  local purple
  local orange
  local green
  local blue
  local red
  # use extended color palette if available
  if [[ -n ${terminfo[colors]} && ${terminfo[colors]} -ge 256 ]]; then
    purple='%F{135}'
    orange='%F{166}'
    green='%F{40}'
    blue='%F{75}'
    red='%F{160}'
  else
    purple='%F{magenta}'
    orange='%F{yellow}'
    green='%F{green}'
    blue='%F{cyan}'
    red='%F{red}'
  fi

  autoload -Uz add-zsh-hook
  autoload -Uz colors && colors

  prompt_opts=(cr percent subst)

  add-zsh-hook precmd prompt_gs_precmd

  zstyle ':zim:git-info' verbose 'yes'
  zstyle ':zim:git-info:branch' format '%b'
  zstyle ':zim:git-info:commit' format '%c'
  zstyle ':zim:git-info:action' format "(${green}%s%f)"
  zstyle ':zim:git-info:unindexed' format "${orange}?"
  zstyle ':zim:git-info:indexed' format "${green}+"
  zstyle ':zim:git-info:untracked' format "${red}-"
  zstyle ':zim:git-info:keys' format \
    'prompt' "(${blue}%b%c%I%i%u%f)%s"

  PROMPT="${purple}%n%f@${orange}%m%f ${green}%~%f\$(prompt_gs_git)\$(prompt_gs_virtualenv) %(!.#.$) "
  RPROMPT='%F{red}%(?..%?)'
}

prompt_gs_setup "$@"
